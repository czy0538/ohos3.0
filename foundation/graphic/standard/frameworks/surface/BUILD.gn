# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/standard/graphic_config.gni")

## Build surface.so {{{
config("surface_config") {
  visibility = [ ":*" ]

  include_dirs = [ "include" ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
  ]
}

config("surface_public_config") {
  include_dirs = [
    "//foundation/graphic/standard/interfaces/innerkits/surface",
    "//foundation/graphic/standard/interfaces/innerkits/common",
    "//third_party/weston/include",
    "//device/rockchip/hardware/gpu/include",
  ]

  if (ace_enable_gpu) {
    defines = [
      "ACE_ENABLE_GPU",
      "ENABLE_EGL",
      "EGL_NO_X11",
    ]
  }
}

ohos_shared_library("surface") {
  sources = [
    "src/buffer_client_producer.cpp",
    "src/buffer_extra_data_impl.cpp",
    "src/buffer_manager.cpp",
    "src/buffer_queue.cpp",
    "src/buffer_queue_consumer.cpp",
    "src/buffer_queue_producer.cpp",
    "src/buffer_utils.cpp",
    "src/consumer_surface.cpp",
    "src/producer_surface.cpp",
    "src/surface.cpp",
    "src/surface_buffer_impl.cpp",
  ]

  if (ace_enable_gpu) {
    sources += [
      "src/egl_data_impl.cpp",
      "src/egl_manager.cpp",
      "src/egl_surface.cpp",
      "src/producer_egl_surface.cpp",
    ]
  }

  configs = [
    ":surface_config",
    "//third_party/libdrm:libdrm_public_config",
  ]

  public_configs = [
    ":surface_public_config",
    "//utils/native/base:utils_config",
  ]

  deps = [
    "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
    "//foundation/distributedschedule/samgr/interfaces/innerkits/samgr_proxy:samgr_proxy",
  ]

  public_deps = [
    "//drivers/peripheral/display/hal:hdi_display_gralloc",
    "//foundation/communication/ipc/interfaces/innerkits/ipc_core:ipc_core",
    "//foundation/graphic/standard/utils:buffer_handle",
    "//utils/native/base:utils",
  ]

  if (ace_enable_gpu) {
    public_deps +=
        [ "//device/rockchip/hardware/gpu:mali-bifrost-g52-g2p0-wayland" ]
  }

  part_name = "graphic_standard"
  subsystem_name = "graphic"
}

## Build surface.so }}}

group("test") {
  testonly = true

  deps = [ "test:test" ]
}
