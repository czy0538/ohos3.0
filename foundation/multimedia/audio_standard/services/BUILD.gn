# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
AUDIO_POLICY_SERVER_DIR =
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server"

ohos_prebuilt_etc("pulseaudio.rc") {
  if (use_musl) {
    source = "etc/pulseaudio.cfg"
  } else {
    source = "etc/pulseaudio.rc"
  }
  relative_install_dir = "init"
  part_name = "multimedia_audio_standard"
  subsystem_name = "multimedia"
}

config("audio_service_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "include",
    "include/server",
    "//foundation/multimedia/audio_standard/frameworks/innerkitsimpl/common/include",
    "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    "//foundation/multimedia/audio_standard/frameworks/innerkitsimpl/audiocapturer/include",
    "//drivers/peripheral/audio/interfaces/include",
    "//drivers/peripheral/audio/hal/hdi_binder/proxy/include",
  ]

  if (target_cpu == "arm") {
    cflags = [ "-DBINDER_IPC_32BIT" ]
  }
}

ohos_shared_library("audio_service") {
  install_enable = true

  sources = [
    "src/server/audio_manager_stub.cpp",
    "src/server/audio_server.cpp",
  ]

  configs = [ ":audio_service_config" ]

  deps = [
    "//foundation/multimedia/audio_standard/frameworks/innerkitsimpl/audiocapturer:audio_capturer_source",
    "//foundation/multimedia/audio_standard/frameworks/innerkitsimpl/pulseaudio/src/daemon:pulseaudio",
    "//foundation/multimedia/audio_standard/interfaces/innerkits/native/audiomanager:audio_client",
    "//utils/native/base:utils",
  ]

  external_deps = [
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr_L2:samgr_proxy",
  ]

  subsystem_name = "multimedia"
  part_name = "multimedia_audio_standard"
}

group("audio_policy_service_packages") {
  deps = [
    ":audio_policy.rc",
    ":audio_policy_config",
    ":audio_policy_service",
  ]
}

ohos_prebuilt_etc("audio_policy.rc") {
  if (use_musl) {
    source = "etc/audio_policy.cfg"
  } else {
    source = "etc/audio_policy.rc"
  }
  relative_install_dir = "init"
  part_name = "multimedia_audio_standard"
  subsystem_name = "multimedia"
}

config("audio_policy_public_config") {
  include_dirs = [
    "//foundation/multimedia/audio_standard/services/include",
    "//foundation/multimedia/audio_standard/services/include/client",
    "//foundation/distributedschedule/safwk/services/safwk/include",
    "//foundation/multimedia/audio_standard/frameworks/innerkitsimpl/common/include",
    "//foundation/multimedia/audio_standard/interfaces/innerkits/native/audioadapter/include",
    "//foundation/multimedia/audio_standard/interfaces/innerkits/native/audiocommon/include",
    "//foundation/multimedia/audio_standard/interfaces/innerkits/native/audiopolicy/include",
    "//foundation/multimedia/audio_standard/interfaces/innerkits/native/audiomanager/include",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/include/common",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/include/interface",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/include/manager",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/include/config",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/include",
    "//foundation/multimedia/audio_standard/services/include/audio_policy/common",
    "//foundation/multimedia/audio_standard/services/include/audio_policy/server",
    "//foundation/multimedia/audio_standard/services/include/audio_policy/client",
    "//foundation/multimedia/audio_standard/frameworks/innerkitsimpl/common/include",
    "//foundation/communication/ipc/interfaces/innerkits/ipc_core/include",
    "//foundation/distributedschedule/samgr/interfaces/innerkits/samgr_proxy/include",
    "//foundation/distributeddatamgr/distributeddatamgr/interfaces/innerkits/distributeddata/include",
    "//base/hiviewdfx/hilog/interfaces/native/innerkits/include",
    "//third_party/libxml2/include",
    "//third_party/pulseaudio/src",
    "//third_party/pulseaudio/confgure/src",
    "//third_party/pulseaudio/include",
    "//foundation/multimedia/audio_standard/frameworks/innerkitsimpl/pulseaudio/include",
    "//utils/system/safwk/native/include",
    "//utils/native/base/include",
    "//third_party/bounds_checking_function/include",
  ]

  cflags = [
    "-Wall",
    "-Werror",
  ]

  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
}

ohos_shared_library("audio_policy_service") {
  install_enable = true
  sources = [
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/audio_policy_manager_stub.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/audio_policy_server.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/src/audio_policy_service.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/src/config/xml_parser.cpp",
    "//foundation/multimedia/audio_standard/services/src/audio_policy/server/service/src/manager/audio_adapter_manager.cpp",
  ]

  public_configs = [ ":audio_policy_public_config" ]

  deps = [
    "//foundation/distributeddatamgr/distributeddatamgr/interfaces/innerkits/distributeddata:distributeddata_inner",
    "//foundation/distributeddatamgr/distributeddatamgr/services/distributeddataservice/adapter:distributeddata_adapter",
    "//foundation/multimedia/audio_standard/frameworks/innerkitsimpl/audioadapter:pulse_audio_service_adapter",
    "//third_party/libxml2:xml2",
    "//utils/native/base:utils",
  ]

  external_deps = [
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
    "safwk:system_ability_fwk",
    "samgr_L2:samgr_proxy",
  ]
  subsystem_name = "multimedia"
  part_name = "multimedia_audio_standard"
}

ohos_prebuilt_etc("audio_policy_config") {
  if (device_name == "rk3566" || device_name == "rk3568") {
    source = "$AUDIO_POLICY_SERVER_DIR/etc/rk3568/audio_policy_config.xml"
  } else {
    source = "$AUDIO_POLICY_SERVER_DIR/etc/audio_policy_config.xml"
  }

  subsystem_name = "multimedia"
  module_install_dir = "etc/audio"
  part_name = "multimedia_audio_standard"
}
